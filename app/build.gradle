// Плагин google-services должен быть перед firebase
plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.appdistribution'
    id 'com.google.firebase.crashlytics'
}

// Для локальных сборок берем ключи из локального хранилища keystore.properties.
// Для удаленных сборок на GitHub Actions берем ключи из Actions Secrets.
// Каталог ../config отсутствует в удаленном репозитории.

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("config/keystore.properties")
if (keystorePropertiesFile.exists()) {
    println "Start extract release keystore config from keystore.properties"
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    println "Start extract release keystore config from global vars"
    keystoreProperties['keyAlias'] = System.env.KEYSTORE_KEY_ALIAS ?: ''
    keystoreProperties['keyPassword'] = System.env.KEYSTORE_KEY_PASSWORD ?: ''
    keystoreProperties['storePassword'] = System.env.KEYSTORE_STORE_PASSWORD ?: ''
    keystoreProperties['storeFile'] = System.env.KEYSTORE_FILE_LOCATION ?: ''
}

android {
    compileSdk rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "org.michaelbel.template"
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
        ndkVersion '21.2.6472646'
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "app" + "-v" + versionName + "(" + versionCode + ")")
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                appId = "1:33042426453:android:f766db4a18b6b79e9102dc" // google-services.json mobilesdk_app_id
                artifactType="APK"
                testers="michaelbel24865@gmail.com"
                releaseNotes="Release Build"
                serviceCredentialsFile = "$rootDir/config/firebase-app-distribution.json"
                //groups="testers"
                //releaseNotesFile="/path/to/releasenotes.txt"
            }
        }
    }

    composeOptions {
        kotlinCompilerVersion rootProject.kotlinCompilerVersion
        kotlinCompilerExtensionVersion rootProject.kotlinCompilerExtensionVersion
    }

    testOptions {
        reportDir = "$rootDir/test-reports"
        resultsDir = "$rootDir/test-results"
    }

    buildFeatures {
        compose true
        viewBinding true

        // Disable unused AGP features
        aidl false
        buildConfig false
        renderScript false
        resValues false
        shaders false
    }
}

dependencies {
    implementation project(':core')

    implementation "androidx.activity:activity-ktx:$activityVersion"
    implementation "androidx.activity:activity-compose:$activityVersion"
    implementation "androidx.annotation:annotation:$annotationVersion"
    implementation "androidx.annotation:annotation-experimental:$annotationExperimentalVersion"
    implementation "androidx.asynclayoutinflater:asynclayoutinflater:$asynclayoutinflaterVersion"
    implementation "androidx.browser:browser:$browserVersion"
    implementation "androidx.cardview:cardview:$cardviewVersion"
    implementation "androidx.collection:collection-ktx:$collectionVersion"
    implementation "androidx.compose.animation:animation:$composeVersion"
    implementation "androidx.compose.compiler:compiler:$composeVersion"
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "androidx.compose.runtime:runtime:$composeVersion"
    implementation "androidx.compose.runtime:runtime-livedata:$composeVersion"
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.ui:ui-viewbinding:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "androidx.core:core-role:$coreroleVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.hilt:hilt-navigation-fragment:$hiltNavigationFragmentVersion"
    implementation "androidx.hilt:hilt-work:$hiltWorkVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
              kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
              kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.savedstate:savedstate-ktx:$savedstateVersion"
    implementation "androidx.sharetarget:sharetarget:$sharetargetVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefreshlayoutVersion"
    implementation "androidx.viewpager2:viewpager2:$viewpager2Version"
    implementation "androidx.work:work-runtime-ktx:$workVersion"

    implementation "com.google.accompanist:accompanist-appcompat-theme:0.18.0"
    implementation "com.google.accompanist:accompanist-coil:0.15.0"
    implementation "com.google.accompanist:accompanist-drawablepainter:0.18.0"
    implementation "com.google.accompanist:accompanist-imageloading-core:0.15.0"
    implementation "com.google.accompanist:accompanist-insets:0.18.0"
    implementation "com.google.accompanist:accompanist-insets-ui:0.18.0"
    implementation "com.google.accompanist:accompanist-glide:0.15.0"
    implementation "com.google.accompanist:accompanist-flowlayout:0.17.0"
    //implementation "com.google.accompanist:accompanist-navigation-animation:0.17.0"
    //implementation "com.google.accompanist:accompanist-navigation-material:0.17.0"
    implementation "com.google.accompanist:accompanist-pager:0.17.0"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.17.0"
    implementation "com.google.accompanist:accompanist-permissions:0.18.0"
    implementation "com.google.accompanist:accompanist-picasso:0.6.2"
    implementation "com.google.accompanist:accompanist-placeholder:0.17.0"
    implementation "com.google.accompanist:accompanist-placeholder-material:0.17.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.17.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.17.0"

    implementation "com.google.dagger:hilt-android:$daggerVersion"
              kapt "com.google.dagger:hilt-compiler:$daggerVersion"
    implementation "com.google.android.exoplayer:exoplayer:$exoplayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-dash:$exoplayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayerVersion"
    implementation "com.google.android.play:core-ktx:$playcoreVersion"
    implementation "com.google.android.gms:play-services-ads:$adsVersion"
    implementation "com.google.android.material:compose-theme-adapter:$composeVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLoggingInterceptorVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "com.ashokvarma.android:gander-persistence:$ganderVersion"
    implementation "com.ashokvarma.android:gander-imdb:$ganderVersion"
    implementation "com.github.kirich1409:viewbindingpropertydelegate:$viewBindingPropertyDelegateVersion"
    implementation "io.coil-kt:coil:$coilVersion"

    //debugImplementation "androidx.fragment:fragment-testing:$fragmentVersion" — not uses exported in manifest

    androidTestImplementation "androidx.arch.core:core-testing:$coreTestingVersion"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeVersion"
    androidTestImplementation "androidx.navigation:navigation-testing:$navigationVersion"
    androidTestImplementation "androidx.test.ext:junit-ktx:$testJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$testEspressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testEspressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$testEspressoVersion"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$testUianimatorVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.work:work-testing:$workVersion"
    androidTestImplementation "com.google.truth:truth:$truthVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"
}